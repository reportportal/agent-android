/*
 * Copyright 2021 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

apply plugin: 'com.github.johnrengelman.shadow'

apply from: "${rootProject.rootDir}/release.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/signing.gradle"

android {
    namespace 'com.epam.reportportal.android.junit5'

    defaultConfig {
        minSdkVersion 26
        compileSdk 34
        targetSdk 34
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    packagingOptions {
        exclude 'META-INF/LICENSE*'
        exclude 'META-INF/INDEX.LIST'
    }
}


configurations {
    internalize
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
task internalizeJar(type: ShadowJar) {
    dependencies {
        include(dependency('com.epam.reportportal:agent-java-junit5'))
    }

    archiveClassifier.set('intern')
    archiveVersion.set('')
    exclude 'agent.properties'

    def pkg = 'com.epam.reportportal.junit5'
    def shadowPrefix = 'android'
    relocate pkg, [shadowPrefix, pkg].join('.')
    configurations = [project.configurations.internalize]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Android Espresso libraries
    api "androidx.test:runner:1.5.2"
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Report Portal libraries
    internalize ('com.epam.reportportal:agent-java-junit5:5.3.0') {
        exclude group: 'org.aspectj' // AspectJ is already included by Android
    }
    api tasks.internalizeJar.outputs.files
    api ('com.epam.reportportal:client-java:5.2.5') {
        exclude group: 'org.aspectj' // AspectJ is already included by Android
    }
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // JUnit 5
    compileOnly "org.junit.jupiter:junit-jupiter-api:${junit5_version}"

    // android-junit5 necessary libraries
    implementation "de.mannodermaus.junit5:android-test-core:${android_junit5_version}"
    implementation "de.mannodermaus.junit5:android-test-runner:${android_junit5_version}"
}

project.afterEvaluate {
    processDebugJavaRes.configure {
        filesMatching("agent.properties") { expand(project.properties) }
    }

    processReleaseJavaRes.configure {
        filesMatching("agent.properties") { expand(project.properties) }
    }
}
